apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'androidx.navigation.safeargs'

def keystoreProperties = new Properties()
def shouldSign = file("../Secrets/KeyStore/keystore.properties").exists()
if (shouldSign) {
    keystoreProperties.load(new FileInputStream(file("../Secrets/KeyStore/keystore.properties")))
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.jaqxues.sniptools"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.0.03"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (shouldSign) {
            release {
                keyAlias = keystoreProperties['keyAlias']
                keyPassword = keystoreProperties['keyPassword']
                storeFile = file(keystoreProperties['keyStorePath'])
                storePassword = keystoreProperties['keyStorePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (shouldSign) {
                signingConfig = signingConfigs.release
            }
        }
    }

    buildFeatures {
        compose true
    }

    // To inline the bytecode built with JVM target 1.8 into
    // bytecode that is being built with JVM target 1.6. (e.g. navArgs)
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }

    dynamicFeatures = [":packimpl"]

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
}

dependencies {


    api fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'com.google.android.material:material:1.2.1'
    api 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    api 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1'
//    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // ViewModel and LiveData
    api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"

    api 'com.jakewharton.timber:timber:4.7.1'

    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    api 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    api "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    api "androidx.room:room-ktx:$room_version"
    api "androidx.preference:preference-ktx:1.1.1"

    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    api('com.github.jaqxues:Akrolyb:332ff125f3')

    api "androidx.compose.ui:ui:$compose_version"
    api "androidx.compose.material:material:$compose_version"
    api "androidx.compose.ui:ui-tooling:$compose_version"
    api "androidx.compose.runtime:runtime-livedata:$compose_version"
//    api "androidx.compose.material:material-icons-extended:$compose_version"
    api "androidx.navigation:navigation-compose:$nav_compose_version"

    api "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
    api "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_version"

    api "com.github.topjohnwu.libsu:core:$libsuVersion"

    // debugApi 'com.amitshekhar.android:debug-db:1.0.6'
    // Files built by fork to allow for 2 different Contexts (+ AndroidX support)
    // https://github.com/jaqxues/Android-Debug-Database
    debugApi files('libs/debug-db-debug.aar')
    debugApi files('libs/debug-db-base-debug.aar')
}
